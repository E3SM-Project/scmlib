#!/bin/csh
## ====================================================================
# Purpose is to modify the (limited area) IOP file that is generated from E3SM
# to remove the lat lon appendecies on the variables.  In addition, the 
# dimension variables need to be renamed to "ncol", which is what E3SM
# expects.  This is a temporary solution until this can be fixed in the E3SM code.

# This script is configured to deal with the minimum number of variables required
# by the E3SM REPLAY option.  Any additional variables will have to be added to the 
# dynvars and physvars array. 

# This script is obviously very hardwired.  Want to submit somthing better?  Please!
# Original (ashamed) author: Peter Bogenschutz (bogenschutz1@llnl.gov)

############################################################
# BEGIN USER INPUT 

# simulation id
setenv simulation E3SM.ne30_ne30.anvil
# file path where replay data is located
setenv filepath /lcrc/group/acme/bogensch/ACME_simulations/{$simulation}/run
# the file containing the E3SM replay data
setenv replayfile {$simulation}.cam.h1.0001-01-01-00000.nc

# Provide name for the IOP file generated by E3SM
setenv file REPLAY.009.ne30_ne30.cam.h1.0001-01-01-00000.nc

# Provide the latlon string that was afixed to all your output 
#  variables in this file.  HINT: to determine, perform a ncdump -h <filename> 
#  and look at the variables names
setenv latlon 40e_to_50e_45s_to_55s

# Full input file name (do not change)
setenv filename {$filepath}/{$replayfile}

# Select a name for the output file that will be generated
setenv finalfile E3SMreplay_{$replayfile}

# Variables that have dimensions of ncol_GLL_$latlon
set dynvars = (lat_GLL lon_GLL CLAT CLDICE_dten CLDLIQ_dten DMS_dten H2O2_dten H2SO4_dten NUMICE_dten NUMLIQ_dten NUMRAI_dten NUMSNO_dten O3_dten Ps Q_dten RAINQM_dten SNOWQM_dten SO2_dten SOAG_dten bc_a1_dten bc_a3_dten bc_a4_dten divT3d dst_a1_dten dst_a3_dten mom_a1_dten mom_a2_dten mom_a3_dten mom_a4_dten ncl_a1_dten ncl_a2_dten ncl_a3_dten num_a1_dten num_a2_dten num_a3_dten num_a4_dten pom_a1_dten pom_a3_dten pom_a4_dten q shflx so4_a1_dten so4_a2_dten so4_a3_dten soa_a1_dten soa_a2_dten soa_a3_dten t u v)

# Variables that have dimension of ncol_physgrid_$latlon
set physvars = (lat_physgrid lon_physgrid Prec Tg Tsair heat_glob lhflx omega phis shflx trefht)

# END USER INPUT
############################################################

# clean up temporary files if they happen to still be lying around
if (-e temp_physics.nc) rm temp_physics.nc
if (-e temp_dynamics.nc) rm temp_dynamics.nc

# determine number of physics variables
set numphysics = ${#physvars}

# Note that the limited area file generated by E3SM has two dimensions,
#  ncol_GLL_$latlon and ncol_physgrid_$latlon.  These are the same!
#  Their names need to be changed to "ncol", which is easy enough to do,
#  but I couldn't figure out how to change both when they exist in the same file
#  without getting an error.  Thus, this script is more complicated that I would 
#  like as variables need to be separated into different files, then 
#  merged back into one file at the end.  

# loop through physics variables and put them into a separate file
set phys_ind = 1
while($phys_ind <= $numphysics)

  ncks -h -A -v {$physvars[$phys_ind]}_$latlon $replayfile temp_physics.nc
  @ phys_ind++

end # variable loop

# determine number of dynamics variables
set numdynamics = ${#dynvars}

# loop through dynamics variables and put them into a separate file
set dyn_ind = 1
while($dyn_ind <= $numdynamics)

  ncks -h -A -v {$dynvars[$dyn_ind]}_$latlon $replayfile temp_dynamics.nc
  @ dyn_ind++

end # variable loop

# bring all other necessary variables to the dynamics file
ncks -h -A -v hyai $replayfile temp_dynamics.nc
ncks -h -A -v hybi $replayfile temp_dynamics.nc
ncks -h -A -v time $replayfile temp_dynamics.nc
ncks -h -A -v date $replayfile temp_dynamics.nc
ncks -h -A -v datesec $replayfile temp_dynamics.nc
ncks -h -A -v time_bnds $replayfile temp_dynamics.nc
ncks -h -A -v time_written $replayfile temp_dynamics.nc
ncks -h -A -v ndbase $replayfile temp_dynamics.nc
ncks -h -A -v nsbase $replayfile temp_dynamics.nc
ncks -h -A -v nbdate $replayfile temp_dynamics.nc
ncks -h -A -v bdate $replayfile temp_dynamics.nc
ncks -h -A -v mdt $replayfile temp_dynamics.nc
ncks -h -A -v ndcur $replayfile temp_dynamics.nc
ncks -h -A -v nscur $replayfile temp_dynamics.nc
ncks -h -A -v co2vmr $replayfile temp_dynamics.nc
ncks -h -A -v ch4vmr $replayfile temp_dynamics.nc
ncks -h -A -v n2ovmr $replayfile temp_dynamics.nc
ncks -h -A -v f11vmr $replayfile temp_dynamics.nc
ncks -h -A -v f12vmr $replayfile temp_dynamics.nc
ncks -h -A -v sol_tsi $replayfile temp_dynamics.nc
ncks -h -A -v tsec $replayfile temp_dynamics.nc
ncks -h -A -v nsteph $replayfile temp_dynamics.nc


##############################################################
#### Rename stuff

# Rename the dimension variable for the dynamics variables to 
#  simply be ncol (what E3SM expects)
ncrename -h -O -d ncol_GLL_$latlon,ncol temp_dynamics.nc 

# Rename all dynamics variables to remove the afixed latlon string
set dyn_ind = 1
while($dyn_ind <= $numdynamics)

  ncrename -h -O -v {$dynvars[$dyn_ind]}_$latlon,{$dynvars[$dyn_ind]} temp_dynamics.nc
  @ dyn_ind++

end # variable loop

# Rename the dimension variable for the physics variables to 
#  simply be ncol (what E3SM expects)
ncrename -h -O -d ncol_physgrid_$latlon,ncol temp_physics.nc 

# Rename all physics variables to remove the afixed latlon string
set phys_ind = 1
while($phys_ind <= $numphysics)

  ncrename -h -O -v {$physvars[$phys_ind]}_$latlon,{$physvars[$phys_ind]} temp_physics.nc
  @ phys_ind++

end # variable loop

# Rename the lat and lon variables to simply be "lat" and "lon"
ncrename -h -O -v lat_physgrid,lat temp_physics.nc
ncrename -h -O -v lon_physgrid,lon temp_physics.nc

# Merge the dynamics file into the physics file
ncks -h -A temp_dynamics.nc temp_physics.nc 

# Remove dynamics GLL variable, put all info into the final file
ncks -h -C -O -x -v lat_GLL,lon_GLL temp_physics.nc $finalfile

# Add this attribute, which is needed for E3SM REPLAY runs
ncatted -h -a CAM_GENERATED_FORCING,global,o,c,"create SCAM IOP dataset" $finalfile

# remove temporary files
rm temp_physics.nc
rm temp_dynamics.nc



